{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "541a9f13-306a-418a-a55d-3b631a6c4fef",
   "metadata": {},
   "source": [
    "# Optimized Meal Planning\n",
    "\n",
    "## Define the model\n",
    "\n",
    "Data\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "T, \\text{the set of discrete time periods} \\\\\n",
    "I, \\text{the set of ingredients that are available} \\\\\n",
    "N, \\text{the set of nutrients that are tracked} \\\\\n",
    "R, \\text{the set of known recipes} \\\\\n",
    "F_{in}, \\text{the amount of nutrient } n \\text{ in ingredient } i,\\ \\forall i \\in I,\\ n \\in N \\\\\n",
    "S_{n}, \\text{the amount of nutrient } n \\text{ that is needed each week for a healthy diet} \\\\\n",
    "C_{i}, \\text{the cost of a single quantity of ingredient } i,\\ \\forall i \\in I \\\\\n",
    "V_{i}, \\text{the volume of a single quantity of ingredient } i,\\ \\forall i \\in I \\\\\n",
    "Fr, \\text{the max volume of the fridge} \\\\\n",
    "Req_{ir}, \\text{the amount of ingredient i needed for recipe} \\ l \\ \\forall i \\in I, r \\in R \\\\\n",
    "MAX, \\text{the maximum number of recipes that can be made in a week}\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "Only consider whether or not to buy an ingredient as an integer variable. The argument for this is that in real life, a recipe can be made with any amount of an ingredient but stores usually sell ingredients in preset sizes.\n",
    "\n",
    "Decision variables:\n",
    "- $x_{it}$, binary variable representing if ingredient i bought in week k, $\\forall i \\in I, t \\in T$\n",
    "- $y_{rt}$, the amount of recipe $l$ to make in week k, $\\forall r \\in R, t \\in T$\n",
    "- $z_{it}$, the amount of ingredient i to store in the fridge in week k, $\\forall i \\in I, t \\in T$\n",
    "\n",
    "Integer Program:\n",
    "\\begin{align}\n",
    "\\min \\sum_{t \\in T} \\sum_{i \\in I} x_{it} * C_i \\\\\n",
    "\\text{s.t. } \\sum_{r \\in R} \\sum_{i \\in I} y_{rt} * Req_{ir} * F_{in} \\geq S_{n}, \\forall t \\in T, n \\in N \\\\\n",
    "\\sum_{i \\in I} z_{it} * V_{i} \\leq Fr, \\forall t \\in T \\\\\n",
    "z_{i0} = 0, \\forall i \\in I \\\\\n",
    "x_{it}*V_i + z_{i,t-1} = \\sum_{r \\in R} y_{rt} * Req_{ir} + z_{it}, \\forall t \\in T, i \\in I \\\\\n",
    "\\sum_{r \\in R} y_{rt} \\leq MAX, \\forall t \\in T \\\\\n",
    "y_{rt}, z_{it} \\geq 0, \\forall i \\in I, t \\in T, x_{it} \\text{ is binary}\n",
    "\\end{align}\n",
    "\n",
    "- The first constraint ensures there are enough nutrients\n",
    "- The second constraint ensures that the fridge is not overfilled\n",
    "- The third constraint sets the intial amount of food to 0\n",
    "- The fourth constraint ensures that the amount of each ingredient being consumed is balanced by the amount being bought\n",
    "- The fifth constraint limits the amount of recipes made in a week (i.e. only 7 recipes worth of recipes being made)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e47b640b-17a7-407a-8289-f9a8cdd89556",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random, NamedArrays, Plots\n",
    "\n",
    "T_end = 10\n",
    "T = 1:T_end\n",
    "FR_CAP = 100\n",
    "\n",
    "#Data 2:\n",
    "Random.seed!(300)\n",
    "\n",
    "# INGREDIENT DATA\n",
    "I = [:rice, :chicken, :steak, :noodles, :cream, :tomato, :asparagus, :bread, :tofu, :banana, :yogurt, :cereal]\n",
    "\n",
    "# C init is now the cost of a single pack of the ingredient - from metromarket.com\n",
    "Cinit = [2, 13, 19, 1.3, 10, 3.8, 6.5, 1.9, 2, 1.6, 1.5, 6.3] # dollars\n",
    "\n",
    "# the cost is accompanied by a volume corresponding to the size of that package\n",
    "V = NamedArray([32, 80, 10, 16, 32, 10, 12, 20, 16, 4, 4.4, 15.4], # oz\n",
    "                I, \"Ingredients\")\n",
    " \n",
    "N = [:calories, :protein, :fiber, :carbs, :fat, :vitamins, :minerals]\n",
    "\n",
    "R = [:steak_rice_asparagus, :chicken_ceasar_salad, :chicken_marinara, \n",
    "    :steak_alfredo, :banana_toast, :overnight_oats, :stirfried_tofu, :clamless_chowder, :CLT]\n",
    "\n",
    "# g / oz\n",
    "F = NamedArray([37 .76 .11 8 .08 1 1;\n",
    "        47 8.8 0 0 1 .25 3;\n",
    "        53 8.3 0 0 2.2 .25 3.5;\n",
    "        45 1.6 .51 8.7 .26 0 .5;\n",
    "        96 .81 0 .81 10.2 1 1;\n",
    "        5 .25 .34 1.1 .06 3 1.5;\n",
    "        6 .62 .6 1.1 .03 2 2;\n",
    "        76 2.5 .77 14 .95 0 1.5;\n",
    "        41 4.9 .65 .79 2.5 1 3;\n",
    "        25 .31 .74 6.5 .09 1.5 3.5;\n",
    "        17 .98 0 1.3 .92 .5 1.25;\n",
    "        92 2.2 3.9 22.4 .45 .75 3;\n",
    "        ], (I, N), (\"Ingredients\", \"Nutrients\"))\n",
    "\n",
    "# INGREDIENT DATA\n",
    "\n",
    "# Each recipe uses about 10 total units of ingredient\n",
    "Req = NamedArray([  5 0 3 0 0 0 3 0 0 0 0 0; # steak rice aspargus\n",
    "                    0 3 0 0 0 2 2 0 0 0 1 0; # chicken ceasar salad\n",
    "                    0 2 0 5 0 3 0 0 0 0 0 0; # chicken marinara\n",
    "                    0 0 3 5 2 0 0 0 0 0 0 0; # streak alfredo\n",
    "                    0 0 0 0 0 0 0 1 0 1 0 0; # banana toast\n",
    "                    0 0 0 0 0 0 0 0 0 1 1 3; # overnight oats\n",
    "                    1 0 0 0 0 0 1 0 1 0 0 0; # stirfried tofu\n",
    "                    0 1 0 0 3 0 0 1 0 0 0 0; # clamless chowder\n",
    "                    0 1 0 0 0 1 0 1 0 0 0 0; # chicken lettuce tomato\n",
    "                    ],\n",
    "                    (R, I), (\"Recipes\", \"Ingredients\")) \n",
    "\n",
    "# nutrition requirements according to Google for average sized adult male\n",
    "# calories: 2000 kcals\n",
    "# protein: 55 g (bhf.org.uk)\n",
    "# fiber: 40 g (health.harvard.edu)\n",
    "# carbs: 130 g (mayoclinic.org)\n",
    "# fat:50\n",
    "# vitamins: 10 (total amount of vitamins needed)\n",
    "# minerals: 10 (total amount of minerals needed)\n",
    "S = NamedArray([2000, 55, 40, 130, 50, 10, 10], N, \"Nutrient Requirements\")\n",
    "\n",
    "C = NamedArray(zeros(Float64, length(T), length(I)), (T, I), (\"Week\", \"Ingredients\"))\n",
    "\n",
    "# Creating prices that randomly over time\n",
    "for i in 1:length(I)\n",
    "    C[1, i] = length(String(I[i]))\n",
    "end\n",
    "\n",
    "for t in 2:length(T)\n",
    "    for i in 1:length(I)\n",
    "        C[t, i] = C[t - 1, i] + .5 * Random.randn() + .025 * C[t - 1, i]\n",
    "        if C[t, i] <= 1\n",
    "            C[t, i] = 1.0\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "# make the model\n",
    "using JuMP, HiGHS\n",
    "\n",
    "m = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(m, x[T, I], Bin)\n",
    "@variable(m, y[T, R] >= 0)\n",
    "@variable(m, z[T, I] >= 0)\n",
    "\n",
    "# Indexing Guide:\n",
    "#\n",
    "# t - time, i - ingredient, r - recipe, n - nutrient\n",
    "# x[t, i], y[t, r], Req[r, i], F[i, n]\n",
    "\n",
    "@constraint(m, nutr_satisfied[n in N, t in T],\n",
    "    sum(y[t, r] * sum(Req[r, i] * F[i, n] for i in I) for r in R) >= S[n])\n",
    "\n",
    "#@constraint(m, enough_ingr_for_recipes[i in I, t in T],\n",
    "  #  x[t, i]*V[i] >= sum( Req[r, i]*y[t, r] for r in R )) \n",
    "\n",
    "@constraint(m, fridge_capacity[t in T], sum( x[t, i]*V[i] for i in I ) <= FR_CAP)\n",
    "\n",
    "@constraint(m, first_ingredient_bal[i in I], x[1, i]*V[i] == sum( y[1,r] * Req[r,i] for r in R) + z[1, i])\n",
    "\n",
    "@constraint(m, ingredient_bal[t in 2:T_end, i in I], x[t, i]*V[i] + z[t-1, i] == sum( y[t,r] * Req[r,i] for r in R) + z[t, i])\n",
    "\n",
    "@objective(m, Min, sum( x[t, i] * C[t, i] for i in I, t in T ))\n",
    "\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "ingredient_values = NamedArray( [ (value(x[t,i])) for t in T, i in I ], (T, I), (\"Week\", \"Ingredients\"))\n",
    "recipe_values = NamedArray( [ (value(y[t,r])) for t in T, r in R ], (T, R), (\"Week\", \"Recipes\"))\n",
    "fridge_values = NamedArray( [ (value(z[t,i])) for t in T, i in I ], (T, I), (\"Week\", \"Stored\"))\n",
    "\n",
    "println(\"Cost Matrix\")\n",
    "println(C)\n",
    "println()\n",
    "\n",
    "p1 = plot(1:10, C, xlabel=\"Week\", ylabel=\"Cost\" , label=permutedims(String.(I)))\n",
    "display(p1)\n",
    "println()\n",
    "\n",
    "println(\"What ingredients to buy in each week\")\n",
    "display(ingredient_values)\n",
    "println()\n",
    "\n",
    "p2 = plot(1:10, ingredient_values, xlabel=\"Week\", ylabel=\"Ingredient bought?\" , label=permutedims(String.(I)))\n",
    "display(p2)\n",
    "println()\n",
    "\n",
    "println(\"Recipes cooked per week\")\n",
    "display(recipe_values)\n",
    "println()\n",
    "\n",
    "p3 = plot(1:10, recipe_values, xlabel=\"Week\", ylabel=\"Quantity of Recipe Cooked\" , label=permutedims(String.(R)))\n",
    "display(p3)\n",
    "println()\n",
    "\n",
    "println(\"How much of each ingredient to store each week\")\n",
    "display(fridge_values)\n",
    "println()\n",
    "\n",
    "p4 = plot(1:10, fridge_values, xlabel=\"Week\", ylabel=\"Quantity Stored\" , label=permutedims(String.(I)))\n",
    "display(p4)\n",
    "println()\n",
    "\n",
    "println(\"Total cost \", objective_value(m))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b11053ef-7d7d-4ebb-bb66-2e47b1777b5a",
   "metadata": {},
   "outputs": [],
   "source": [
    "#using DataFrames, CSV\n",
    "#df = CSV.read(\"stigler.csv\",DataFrame,delim=',')"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
